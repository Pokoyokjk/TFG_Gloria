PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX mls: <http://www.w3.org/ns/mls#>
PREFIX onyx: <http://www.gsi.dit.upm.es/ontologies/onyx/ns#>
PREFIX oro: <http://kb.openrobots.org#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX segb: <http://www.gsi.upm.es/ontologies/segb/ns#>

SELECT ?model ?dataset ?software ?algorithm 
    ?robotName ?humanName
WHERE {
?listening a oro:ListeningEvent, 
        segb:LoggedActivity ;
    oro:hasSpeaker ?human ;
    oro:hasListener ?robot .
?robot a oro:Robot ;
    oro:hasName ?robotName .
?human a prov:Person, foaf:Person ;
    foaf:firstName ?humanName .
?msg a oro:Message, prov:Entity ;
    prov:wasGeneratedBy ?listening .
?emotionAnalysis a onyx:EmotionAnalysis, 
        segb:LoggedActivity ;
    prov:used ?msg ;
    segb:usedMLModel ?model .
?model a mls:Model .
?dataset a mls:Dataset .
?run a mls:Run;
    mls:hasOutput ?model ;
    mls:hasInput ?dataset ;
    mls:executes ?implementation ;
    mls:realizes ?algorithm .
?software a mls:Software ;
    mls:hasPart ?implementation .
}